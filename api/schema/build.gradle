//  Licensed to the Apache Software Foundation (ASF) under one
//  or more contributor license agreements.  See the NOTICE file
//  distributed with this work for additional information
//  regarding copyright ownership.  The ASF licenses this file
//  to you under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing,
//  software distributed under the License is distributed on an
//  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
//  KIND, either express or implied.  See the License for the
//  specific language governing permissions and limitations
//  under the License.

description = 'Apache Isis Core - Schemas'

plugins {
    id "org.openrepose.gradle.plugins.jaxb" version "2.5.0"
}
apply plugin: 'java-library'

def episodesDir = "${project.buildDir}/generated-sources/xjc/META-INF"
def javaVersion = JavaVersion.current()

sourceSets {
    main {
        java {
            srcDirs += "${buildDir}/generated-sources/xjc"
        }
    }
}

dependencies {
    compile project(':org.apache.isis.commons:isis-commons')

    compile(Libs.springContext)
    compile(Libs.jodaTime)
    
    if (javaVersion > JavaVersion.VERSION_1_8) {
    // These dependencies are required in order to build on jdk versions > 1.8
    	api(Libs.jaxbApi)
    	implementation(Libs.jaxbImpl)
    	implementation(Libs.jaxbCore)
    }
    
    // jaxb plugin dependencies
    jaxb(Libs.jaxbApi)
    jaxb(Libs.jaxbCore)
    jaxb(Libs.jaxbImpl)
    jaxb(Libs.jaxbXjc)
    jaxb("org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.3")
}

task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}
artifacts.archives packageTests

jaxb {
	xsdDir = "${project.projectDir}/src/main/resources/org/apache/isis/schema"
 	xsdIncludes = ['**/*.xsd']
 	bindingsDir = "${project.projectDir}/src/main/resources/org/apache/isis/schema"
 	bindings = ['*.xml']
 	xjc {
 		accessExternalSchema = 'file'
 		args = ["-Xnamespace-prefix"]
 	}
}

xjc.configure {
	doLast {
	  	mkdir "${episodesDir}"
	  	copy {
		    from file("${project.buildDir}/generated-resources/episodes/isis.apache.org-schema-common.episode")
		    into "${episodesDir}"
		}
	}
}

compileJava {
	dependsOn xjc
}

// required due to bug 
// https://github.com/rackerlabs/gradle-jaxb-plugin/issues/36
tasks.named("xsd-dependency-tree").configure {
    outputs.upToDateWhen { false }
}
